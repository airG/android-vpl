apply plugin: 'com.android.library'

apply from: "$rootDir/version.gradle"
apply from: "$rootDir/dependencies.gradle"

logger.quiet("android-vpl-$VERSION_NAME ($VERSION_CODE)")

ext {
    bintrayOrg = 'airgoss'
    bintrayRepo = 'airGOss'
    bintrayName = 'vpl'

    publishedGroupId = 'com.airg.android'
    libraryName = 'Virtual PreLoad'
    artifact = 'vpl'

    libraryDescription = 'Virtual preload helper library'

    siteUrl = 'https://github.com/airG/android-vpl'
    gitUrl = 'https://github.com/airG/android-vpl.git'

    libraryVersion = VERSION_NAME

    developerId = 'airgoss'
    developerName = 'airG OpenSource'
    developerEmail = 'opensource@airg.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    // do not modify. These fields are auto generated in dependencies.gradle
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        // do not modify. These fields are auto generated in dependencies.gradle
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion COMPILE_SDK_VERSION

        // do not modify. These fields are auto generated in version.gradle
        versionCode VERSION_CODE
        versionName VERSION_NAME

        buildConfigField "String", "VCS_BRANCH", "\"$VCS_BRANCH\""
        buildConfigField "String", "VCS_REVISION", "\"$VCS_REVISION\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
        }
    }

    productFlavors {
        mixpanel {}
        google {}
        fabric {}
    }

    publishNonDefault true
}

dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile "com.android.support:support-annotations:$SUPPORT_LIB_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIB_VERSION"
    compile "com.android.support:support-v4:$SUPPORT_LIB_VERSION"

    fabricCompile('com.crashlytics.sdk.android:answers:1.3.12@aar') {
        transitive = true;
    }
    mixpanelCompile "com.mixpanel.android:mixpanel-android:4.9.8"
    googleCompile "com.google.android.gms:play-services-analytics:${GMS_VERSION}"
    testCompile 'junit:junit:4.12'
}

apply from: 'https://raw.githubusercontent.com/airG/JCenter/master/install.gradle'
apply from: 'https://raw.githubusercontent.com/airG/JCenter/master/bintray.gradle'